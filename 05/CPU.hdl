// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Determine if A register will load address (A-instruction), or ALU Output (C-intruction)
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=aOrCOut);

    // Determine if selected value is loaded into A Register (A-instruction: always load; C-instruction: load when d1=1)
    Mux4Way(a=true, b=true, c=false, d=true, sel[1]=instruction[15], sel[0]=instruction[5], out=loadARegister);
    ARegister(in=aOrCOut, load=loadARegister, out=outA, out[0..14]=addressM);

    // Select A or M to ALU y input
    Mux16(a=outA, b=inM, sel=instruction[12], out=outAorM);
    
    // Determine if load ALU output into D Register
    And(a=instruction[4], b=instruction[15], out=loadD);
    DRegister(in=aluOut, load=loadD, out=outD);

    // ALU
    ALU(x=outD, y=outAorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=aluZR, ng=aluNG);

    // Determine if writeM = 1 for C instructions (write to Memory). Always set to 0 if A-instruction
    And(a=instruction[15], b=instruction[3], out=writeM);

    // Jump control
    Nor(a=aluZR, b=aluNG, out=outNor);
    Or(a=aluZR, b=outNor, out=inD);
    Not(in=aluZR, out=inF);
    Or(a=aluZR, b=aluNG, out=inG);    
    Mux8Way(a=false, b=outNor, c=aluZR, d=inD, e=aluNG, f=inF, g=inG, h=true, sel[2]=instruction[2], sel[1]=instruction[1], sel[0]=instruction[0], out=jump);
    And(a=instruction[15], b=jump, out=loadPC);
    Not(in=loadPC, out=incIN);
    PC(in=outA, load=loadPC, inc=incIN, reset=reset, out[0..14]=pc);


}